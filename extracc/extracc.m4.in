#
# Configure path for the @PACKAGE_NAME@ library.
# Denis Arnaud <denis_arnaud@users.sourceforge.net>, July 2008
#
# Note: as the @PACKAGE_NAME@ library depends upon BOOST to build,
# your configure.ac must define appropriately the BOOST_CFLAGS
# variable.
#


AC_DEFUN([AM_PATH_@PACKAGE_NAME@],
[
	AC_LANG_SAVE
	AC_LANG([C++])

	AC_ARG_WITH(@PACKAGE@,
		[ --with-@PACKAGE@=PFX   Prefix where @PACKAGE_NAME@ is installed (optional, default /usr/local)],
		@PACKAGE@_dir="$withval",
		@PACKAGE@_dir="/usr/local")

	@PACKAGE_NAME@_CONFIG="$@PACKAGE@_dir/bin/@PACKAGE@-config"

	AS_IF([test -d "$@PACKAGE@_dir" && test -x "$@PACKAGE_NAME@_CONFIG"],
		[],
		[AC_MSG_FAILURE([--with-@PACKAGE@ must be a valid directory where @PACKAGE_NAME@ is installed (default /usr/local)])])

	@PACKAGE_NAME@_VERSION=`$@PACKAGE_NAME@_CONFIG --version`
	@PACKAGE_NAME@_CFLAGS=`$@PACKAGE_NAME@_CONFIG --cflags`
	@PACKAGE_NAME@_LIBS=`$@PACKAGE_NAME@_CONFIG --libs`

	AC_SUBST([@PACKAGE_NAME@_VERSION])
	AC_SUBST([@PACKAGE_NAME@_CFLAGS])
	AC_SUBST([@PACKAGE_NAME@_LIBS])
	
	@PACKAGE_NAME@_EXTRACPPUNIT_INCLUDE=`$@PACKAGE_NAME@_CONFIG --extracppunit-makefile-include`
	AC_SUBST([@PACKAGE_NAME@_EXTRACPPUNIT_INCLUDE])

	@PACKAGE@_major_version=`echo $@PACKAGE_NAME@_VERSION | sed 's/^\([[0-9]]*\).*/\1/'`
	if test "x${@PACKAGE@_major_version}" = "x" ; then
		@PACKAGE@_major_version=0
	fi

	@PACKAGE@_minor_version=`echo $@PACKAGE_NAME@_VERSION | \
						sed 's/^\([[0-9]]*\)\.\{0,1\}\([[0-9]]*\).*/\2/'`
	if test "x${@PACKAGE@_minor_version}" = "x" ; then
		@PACKAGE@_minor_version=0
	fi

	@PACKAGE@_micro_version=`echo $@PACKAGE_NAME@_VERSION | \
	sed 's/^\([[0-9]]*\)\.\{0,1\}\([[0-9]]*\)\.\{0,1\}\([[0-9]]*\).*/\3/'`
	if test "x${@PACKAGE@_micro_version}" = "x" ; then
		@PACKAGE@_micro_version=0
	fi

AC_LANG_RESTORE
])
